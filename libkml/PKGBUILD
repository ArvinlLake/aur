# Maintainer: Eugen Zagorodniy <https://github.com/ezag>

pkgname=libkml
pkgver=1.2.0
pkgrel=2
pkgdesc="A KML library written in C++ with bindings to other languages"
arch=(i686 x86_64)
url="http://code.google.com/p/$pkgname/"
license=('BSD')
depends=('expat' 'uriparser')
optdepends=('swig: bindings for additional languages'
            'python2: python bindings'
            'jdk: java bindings')
# TODO: Use existing libminizip package instead of conflicting with it.
conflicts=('libminizip')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "swig-version-check.patch")
md5sums=('25d534437f971bf55a945608e39fea79'
         'ba79679b85f24c5e6e82241683ea4a0f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Take into account major version number when checking version of SWIG
  # in configure script.
  patch < $srcdir/swig-version-check.patch

  # '--Wno-long-long' works around warning (with -Wall treated as error)
  # in 'third_party/boost_1_34_1/boost/config/suffix.hpp', lines 435-436.
  #
  # '--no-as-needed' works around 'undefined reference' errors in
  # 'examples/helloworld'.
  #
  # Add --disable-python or --disable-java to configure flags if your
  # system doesn't have corresponding language.

  CXXFLAGS+=-Wno-long-long LDFLAGS+=-Wl,--no-as-needed PYTHON=python2 \
  ./configure --prefix=/usr --enable-systempython \
              --with-python-include-dir=/usr/include/python2.7 \
              --with-python-lib-dir=/usr/lib/python2.7

  # See http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=667247
  sed -i '34i #include <unistd.h>' src/kml/base/file_posix.cc

  make 

  # TODO: Use installed packages instead of contents of third_party
  # directory if possible.

  # TODO: Either fix 'WARNING: Package contains reference to $srcdir' or
  # ensure that it is harmless.
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # TODO: Remove uriparser sources before building and use existing package.
  rm "$pkgdir/usr/lib/liburiparser."{{,l}a,so{,.1{,.0.5}}}
  mkdir -p "$pkgdir/share/licenses/$pkgname"
  cp COPYING "$pkgdir/share/licenses/$pkgname/"
}
