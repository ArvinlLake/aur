# Maintainer:  Eugen Zagorodniy <e dot zagorodniy at gmail dot com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname='pidgin-zag'
pkgver=2.10.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
pkgdesc="Pidgin with patch for ticket 10593"
depends=('libpurple' 'startup-notification' 'gtkspell' 'libxss' 'libsm'
         'gstreamer0.10' 'hicolor-icon-theme')
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'libsm'
             'libidn' 'python2' 'hicolor-icon-theme' 'gstreamer0.10'
             'farsight2' 'avahi' 'tk' 'ca-certificates' 'intltool'
             'networkmanager')
optdepends=('aspell: for spelling correction'
            'ca-certificates: SSL CA certificates'
            'gstreamer0.10-good-plugins: video and voice support'
            'tk: Tcl/Tk scripting support')
provides=('pidgin')
conflicts=('pidgin')
options=('!libtool')
install=pidgin.install
source=(http://downloads.sourceforge.net/pidgin/pidgin-$pkgver.tar.bz2
        nm09-more.patch
        http://developer.pidgin.im/raw-attachment/ticket/10593/pidgin-ticket-10593.patch)
sha256sums=('2f28bddc5edcd714d607d74126c8958ae7c258602b3929e3e6e783d3cb1beac8'
            '7e173ea37b86b604284da18ea015ee03ccd444f3e980f440c201957d37ee820e'
            '1947fb4992264322d37b5ad0ee4b2a764fc12c98077e94b9b4cd970416511988')
build() {
  cd "$srcdir/pidgin-$pkgver"

  # Further fixes for NetworkManager 0.9
  # http://developer.pidgin.im/ticket/13859
  patch -Np1 -i "$srcdir/nm09-more.patch"

  # #10593: Tray notification for jabber groupchats
  # http://developer.pidgin.im/ticket/10593
  patch -Np1 -i "$srcdir/pidgin-ticket-10593.patch"

  # Use Python 2
  sed -i 's/env python$/&2/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  # Pidgin doesn't explicitly link to libm
  LDFLAGS+=' -Wl,--copy-dt-needed-entries'

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-meanwhile \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C pidgin DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES

  install -Dm644 pidgin.desktop "$pkgdir"/usr/share/applications/pidgin.desktop

  rm "$pkgdir/usr/share/man/man1/finch.1"
}
