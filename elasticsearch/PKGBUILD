# Maintainer: Eugen Zagorodniy e dot zagorodniy at gmail dot com
pkgname=elasticsearch
pkgver=0.17.8
pkgrel=1
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('i686' 'x86_64')
url="http://www.elasticsearch.org/"
license=('APACHE')
depends=('java-runtime')
install='elasticsearch.install'
source=(
  "https://github.com/downloads/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
)
md5sums=('fb02e4e15f27a395903b4a75bbd7e927')

build() {
  cd $srcdir
  wget https://github.com/$pkgname/$pkgname-servicewrapper/tarball/master \
       -O $pkgname-servicewrapper.tar.gz --no-check-certificate
  tar xf $pkgname-servicewrapper.tar.gz
  mv *-$pkgname-servicewrapper-*/service $srcdir/$pkgname-$pkgver/bin/
}

package() {
  mkdir $pkgdir/opt
  mv $srcdir/$pkgname-$pkgver $pkgdir/opt/$pkgname
  cd $pkgdir/opt/$pkgname
  mkdir lock

  # Remove unnesessary libraries
  case $CARCH in
    i686) _bits=32 ;;
    x86_64) _bits=64 ;;
    *) echo "Unknown architecture '$CARCH'"; exit 1
  esac
  find lib/sigar/* ! -name '*.jar' ! -name '*x86-linux.so' -delete
  find bin/service/lib/* ! -name '*.jar' ! -name "*linux-x86-$_bits.so" -delete
  find bin/service/exec/* ! -name "*linux-x86-$_bits" -delete

  # Configure service wrapper
  sed -e 's/^#\(RUN_AS_USER=\).*$/\1elasticsearch/'                     \
      -e 's_^\(LOCKDIR=\).*$_\1"/opt/elasticsearch/lock/"_'             \
      -e "/^WRAPPER_TEST_CMD=\"\"$/iDIST_ARCH=x86; DIST_BITS=$_bits"    \
      -i bin/service/elasticsearch
  sed -e 's/^\(set\.default\.ES_HOME=\).*$/\1\/opt\/elasticsearch/'     \
      -e 's/^\(wrapper\.java\.command=\).*$/\1\/opt\/java\/bin\/java/'  \
      -i bin/service/elasticsearch.conf

  # Set proper permissions
  chmod +x bin/service/exec/*

  # Install rc.d script
  mkdir -p $pkgdir/etc/rc.d
  ln -s /opt/elasticsearch/bin/service/elasticsearch $pkgdir/etc/rc.d
}

