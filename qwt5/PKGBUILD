# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=qwt
pkgver=5.2.1
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt>=4.5.1')
options=('!makeflags')
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/sourceforge/qwt/$pkgname-$pkgver.tar.bz2" \
        "qwtconfig-archlinux.pri")
md5sums=('4a595b8db0ec3856b117836c1d60cb27'
         '2cb9135ec875d6de0214fcb032f184a3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # copy our config file to the right place
  install -Dm644 ${srcdir}/qwtconfig-archlinux.pri \
	${srcdir}/${pkgname}-${pkgver}/qwtconfig.pri

  # build qwt:
  qmake qwt.pro
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install

  # fix doc files
  install -d ${pkgdir}/usr/share/doc/${pkgname}
  mv -f ${pkgdir}/usr/share/html/* ${pkgdir}/usr/share/doc/${pkgname}
  
  # install licence
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
