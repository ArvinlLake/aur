# Maintainer:  Eugen Zagorodniy <e dot zagorodniy at gmail dot com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=qwt5
pkgver=5.2.1
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications (version 5.X)"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt>=4.5.1')
options=('!makeflags')
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/sourceforge/qwt/qwt-$pkgver.tar.bz2" \
        "qwtconfig-archlinux.pri")
md5sums=('4a595b8db0ec3856b117836c1d60cb27'
         '15db52c3312b5f8f7cbbdabc4831591b')
build() {
  cd ${srcdir}/qwt-${pkgver}

  # copy our config file to the right place
  install -Dm644 ${srcdir}/qwtconfig-archlinux.pri \
	${srcdir}/qwt-${pkgver}/qwtconfig.pri

  # build qwt:
  qmake qwt.pro
  make || return 1
}

package() {
  cd ${srcdir}/qwt-${pkgver}

  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install

  # rename conlicting symlink
  mv ${pkgdir}/usr/lib/libqwt.so ${pkgdir}/usr/lib/libqwt5.so


  # fix doc files
  install -d ${pkgdir}/usr/share/doc/${pkgname}
  mv -f ${pkgdir}/usr/share/html/* ${pkgdir}/usr/share/doc/${pkgname}
  
  # install licence
  install -Dm644 ${srcdir}/qwt-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
